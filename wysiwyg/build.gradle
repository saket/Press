apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'kotlin-android-extensions'
apply plugin: "org.jetbrains.kotlin.native.cocoapods"
apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
  compileSdkVersion versions.compileSdk
  defaultConfig {
    minSdkVersion versions.minSdk
  }

  lintOptions {
    lintConfig file("lint.xml")
  }
}

kotlin {
  targets {
    fromPreset(presets.android, "android")
    fromPreset(presets.macosX64, "macos")

    def buildForDevice = project.findProperty("device")?.toBoolean() ?: false
    def iosTarget = (buildForDevice) ? presets.iosArm64 : presets.iosX64

    fromPreset(iosTarget, "ios") {
      compilations.main.kotlinOptions.freeCompilerArgs += "-Xobjc-generics"
      [binaries.getFramework('debug'), binaries.getFramework('release')].each {
        // Disable bitcode embedding for the simulator build.
        if (!buildForDevice) {
          it.embedBitcode("disable")
        }
      }
    }
  }

  sourceSets {
    commonMain {
      dependencies {}
    }
    commonTest {
      dependencies {
        implementation kotlin('test-common')
        implementation kotlin('test-annotations-common')
        implementation deps.assertK.common
        implementation deps.assertK.opentest4j  // https://youtrack.jetbrains.com/issue/IDEA-214533
      }
    }
    androidMain {
      dependencies {
        implementation deps.androidx.appCompat
        implementation deps.flexmark.core
        implementation deps.flexmark.strikethrough
        implementation deps.flexmark.autoLink
        implementation deps.timber
      }
    }
    androidTest {
      dependencies {
        implementation kotlin('test')
        implementation kotlin('test-junit')
      }
    }
    macosMain {}
    macosTest {}
    iosMain {}
    iosTest {}

    all {
      languageSettings {
        enableLanguageFeature('InlineClasses')
      }
    }
  }
}

kotlin.cocoapods {
  summary = "A cross-platform markdown parser"
  homepage = "https://github.com/saket/press/tree/trunk/wysiwyg"
}
version = 1.0 // also used by cocoapods plugin.

dependencies {}
